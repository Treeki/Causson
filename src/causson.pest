WHITESPACE = _{" " | "\t" | ("\\" ~ NEWLINE)}
COMMENT = _{"--" ~ (!NEWLINE ~ ANY)*}

id = @{identifierStart ~ identifierChar* ~ ("#" ~ (identifierChar | idAllowedOpChars)*)?}
  identifierStart = _{XID_START | "_"}
  identifierChar = _{XID_START | XID_CONTINUE | "_" | "="}
  idAllowedOpChars = _{"+" | "-" | "*" | "/" | "<" | ">" | "!" | "&" | "|"}

qualifiedID = {(id ~ ":")* ~ id}

typeRef = {qualifiedID ~ ("<" ~ (typeRef ~ ",")* ~ typeRef ~ ">")?}
typedID = {typeRef ~ id}

// Expressions
expr = {term ~ (operator ~ term)*}
  term = {termPiece ~ termSuffix*}
    termPiece = _{ifTerm | whileTerm | letTerm | bTrue | bFalse | id | real | int | string | ("(" ~ expr ~ ")")}
      int = @{("+" | "-")? ~ ASCII_DIGIT+}
      real = @{int ~ "." ~ ASCII_DIGIT*}
      string = ${"\"" ~ stringInner ~ "\""}
        stringInner = @{stringChar*}
        stringChar = {!("\"") ~ ANY}
      bTrue = {"true"}
      bFalse = {"false"}
      ifTerm = {"if" ~ expr ~ "{" ~ codeBlock ~ "}" ~ ("elif" ~ expr ~ "{" ~ codeBlock ~ "}")* ~ ("else" ~ "{" ~ codeBlock ~ "}")?}
      whileTerm = {"while" ~ expr ~ "{" ~ codeBlock ~ "}"}
      letTerm = {"let" ~ id ~ "=" ~ expr}
    termSuffix = _{termCall | termPropAccess | termNamespaceAccess | termSpecialise}
      termCall = {("(" ~ ")") | ("(" ~ expr ~ ("," ~ expr)* ~ ")")}
      termPropAccess = {"." ~ id}
      termNamespaceAccess = {":" ~ id}
      termSpecialise = {"<" ~ typeRef ~ ">"}
  operator = _{add | subtract | multiply | divide | eq | ne | le | lt | ge | gt | assign | logAnd | logOr}
    add = {"+"}
    subtract = {"-"}
    multiply = {"*"}
    divide = {"/"}
    eq = {"=="}
    ne = {"!="}
    lt = {"<"}
    le = {"<="}
    gt = {">"}
    ge = {">="}
    assign = {"="}
    logAnd = {"&&"}
    logOr = {"||"}

codeBlock = {NEWLINE* ~ (expr ~ NEWLINE+)* ~ expr?}

// Global Definitions
gTypeDef = {"type" ~ qualifiedID ~ "=" ~ typeDef}
  typeDef = _{enumDef | wrapDef | recordDef}
    enumDef = {"enum" ~ "(" ~ (enumValue ~ ",")* ~ enumValue ~ ","? ~ ")"}
      enumValue = {id ~ ("(" ~ (typedID ~ ",")* ~ (typedID ~ ","?)? ~ ")")?}
    wrapDef = {"wrap" ~ typeRef}
    recordDef = {"record" ~ "{" ~ NEWLINE* ~ (typedID ~ (NEWLINE | ",")+)* ~ (typedID ~ (NEWLINE | ",")?)? ~ "}"}

gFuncDef = {"def" ~ funcSpec ~ "->" ~ typeRef ~ "{" ~ codeBlock ~ "}"}
  funcSpec = {qualifiedID ~ (("(" ~ funcSelfArg? ~ ")") | ("(" ~ (funcSelfArg ~ ",")? ~ (typedID ~ ",")* ~ typedID ~ ")"))}
    funcSelfArg = {"self"}

gComponentDef = {"component" ~ qualifiedID ~ "{" ~ NEWLINE* ~ (compSubDef ~ NEWLINE+)* ~ compSubDef? ~ "}"}
  compSubDef = _{compMethod | compPropSet | compEventConnection | compInstance | compTransientAdd}
    compPropSet = {"." ~ id ~ "=" ~ expr}
    compTransientAdd = {"+" ~ expr}
    compEventConnection = {"." ~ id ~ "->" ~ (expr | ("{" ~ codeBlock ~ "}"))}
    compInstance = {(id ~ "=")? ~ typeRef ~ compInstanceArgs? ~ ("{" ~ NEWLINE* ~ (compSubDef ~ NEWLINE+)* ~ compSubDef? ~ "}")?}
      compInstanceArgs = {"(" ~ (expr ~ ",")* ~ expr? ~ ")"}
    compMethod = {"def" ~ compMethodSpec ~ "->" ~ typeRef ~ "{" ~ codeBlock ~ "}"}
      compMethodSpec = {id ~ (("(" ~ ")") | ("(" ~ (typedID ~ ",")* ~ typedID ~ ")"))}

globalDef = {gTypeDef | gFuncDef | gComponentDef}

//program = _{ SOI ~ NEWLINE* ~ (globalDef ~ NEWLINE+)* ~ EOI }
program = _{ SOI ~ NEWLINE* ~ (globalDef ~ NEWLINE+)* ~ globalDef? ~ EOI }
